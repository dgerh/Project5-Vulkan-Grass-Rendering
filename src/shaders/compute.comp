#version 450
#extension GL_ARB_separate_shader_objects : enable

//use defines
#define USE_WIND true
#define USE_GRAVITY true
#define USE_RECOVERY true

#define USE_ORIENTATION_CULLING true
#define USE_FRUSTUM_CULLING true
#define USE_DISTANCE_CULLING true

//constants
#define PI_OVER_FOUR 0.78539816339f

//force constants
#define GRAVITY_STRENGTH 1.f
#define WIND_STRENGTH 20.f
#define WIND_FREQUENCY 0.5f

//culling constants
#define ORIENTATION_CULL_VAL 0.9f
#define FRUSTUM_CULL_VAL 0.05f
#define MAX_DISTANCE 40
#define NUM_BUCKETS 10

#define WORKGROUP_SIZE 32
layout(local_size_x = WORKGROUP_SIZE, local_size_y = 1, local_size_z = 1) in;

layout(set = 0, binding = 0) uniform CameraBufferObject {
    mat4 view;
    mat4 proj;
    vec4 pos;
} camera;

layout(set = 1, binding = 0) uniform Time {
    float deltaTime;
    float totalTime;
};

struct Blade {
    vec4 v0;
    vec4 v1;
    vec4 v2;
    vec4 up;
};

// DONE: Add bindings to:
// 1. Store the input blades
// 2. Write out the culled blades
// 3. Write the total number of blades remaining

layout (set = 2, binding = 0) buffer InputBlades {
    Blade[] inputBlades;
};

layout (set = 2, binding = 1) buffer OutputBlades {
    Blade[] outputBlades;
};

// The project is using vkCmdDrawIndirect to use a buffer as the arguments for a draw call
// This is sort of an advanced feature so we've showed you what this buffer should look like
//
layout(set = 2, binding = 2) buffer NumBlades {
    uint vertexCount;   // Write the number of blades remaining here
    uint instanceCount; // = 1
    uint firstVertex;   // = 0
    uint firstInstance; // = 0
} numBlades;

bool inBounds(float value, float bounds) {
    return (value >= -bounds) && (value <= bounds);
}

vec3 random3( vec3 p ) {
    return fract(sin(vec3(dot(p, vec3(127.1, 311.7, 231.42)),
                          dot(p, vec3(269.5, 183.3, 256.77)),
                          dot(p, vec3(420.6, 631.2, 921.45))
                    )) * 43758.5453);
}

float surflet(vec3 p, vec3 gridPoint) {
    // Compute the distance between p and the grid point along each axis, and warp it with a
    // quintic function so we can smooth our cells
    vec3 t2 = abs(p - gridPoint);
    vec3 t = vec3(1.f) - 6.f * (t2 * t2 * t2 * t2 * t2) + 15.f * (t2 * t2 * t2 * t2) - 10.f * (t2 * t2 * t2);
    // Get the random vector for the grid point (assume we wrote a function random2
    // that returns a vec2 in the range [0, 1])
    vec3 gradient = random3(gridPoint) * 2. - vec3(1., 1., 1.);
    // Get the vector from the grid point to P
    vec3 diff = p - gridPoint;
    // Get the value of our height field by dotting grid->P with our gradient
    float height = dot(diff, gradient);
    // Scale our height field (i.e. reduce it) by our polynomial falloff function
    return height * t.x * t.y * t.z;
}

float perlinNoise3D(vec3 p) {
	float surfletSum = 0.f;
	// Iterate over the four integer corners surrounding uv
	for(int dx = 0; dx <= 1; ++dx) {
		for(int dy = 0; dy <= 1; ++dy) {
			for(int dz = 0; dz <= 1; ++dz) {
				surfletSum += surflet(p, floor(p) + vec3(dx, dy, dz));
			}
		}
	}
	return surfletSum;
}

void main() {
	// Reset the number of blades to 0
	if (gl_GlobalInvocationID.x == 0) {
		numBlades.vertexCount = 0;
	}
	barrier(); // Wait till all threads reach this point

    // TODO: Apply forces on every blade and update the vertices in the buffer
    Blade currBlade = inputBlades[gl_GlobalInvocationID.x];

    vec3 v0 = currBlade.v0.xyz;
    vec3 v1 = currBlade.v1.xyz;
    vec3 v2 = currBlade.v2.xyz;
    vec3 up = currBlade.up.xyz;

    float theta = currBlade.v0.w;
    vec3 f = vec3(cos(theta + PI_OVER_FOUR), 0, sin(theta + PI_OVER_FOUR));
    float stiffness = currBlade.up.w;
    float width = currBlade.v1.w;

    //gravity
    vec3 g_force = vec3(0.f);
    if (USE_GRAVITY) {
        vec4 D = vec4(0.f, -1.f, 0.f, GRAVITY_STRENGTH);
        vec3 gE = normalize(D.xyz) * D.w;
        vec3 gF = 0.25 * length(gE) * f;
        g_force = gE + gF;
    }
    
    //recovery
    vec3 r_force = vec3(0.f);
    if (USE_RECOVERY) {
        vec3 iv2 = v0 + width * up;
        r_force = (iv2 - v2) * stiffness;
    }

    //wind
    vec3 w_force = vec3(0.f);
    if (USE_WIND) {
        float scaledTime = totalTime * WIND_FREQUENCY;
        vec3 unifiedWindDir = vec3(1.f, 0.f, 1.f);
        vec3 windDir = vec3(perlinNoise3D(vec3(v2.x * 0.235, v2.y * 0.73, v2.z * 1.2) + unifiedWindDir * scaledTime),
                            0.f,
                            perlinNoise3D((vec3(v2.x * 0.732, v2.y * 0.11, v2.z * 0.5) * 0.532f + vec3(214.f, 11.f, 90.f)) + unifiedWindDir * scaledTime));
        windDir *= WIND_STRENGTH;
        float windAlignment = 1.f - dot(windDir, (v2 - v0) / (v2 - v0).length());
        w_force = windDir * windAlignment;
    }

    //apply forces
    vec3 deltaForce = (r_force + g_force + w_force) * deltaTime;
    v2 += deltaForce;
    v2 = v2 - up * min(up * (v2 - v0), 0);
    float lProj = length(v2 - v0 - up * dot(v2 - v0, up));
    v1 = v0 + width * up * max(1.f - lProj / width, 0.05 * max(lProj / width, 1.f));

    float L0 = length(v2 - v0);
    float L1 = length(v1 - v0) + length(v2 - v1);

    float n = 3.f;
    float L = (2.f * L0 + (n - 1.f) * L1) / (n + 1.f);

    float r = width / L;
    vec3 v1_corr = v0 + r * (v1 - v0);
    vec3 v2_corr = v1_corr + r * (v2 - v1);

    currBlade.v1.xyz = v1_corr.xyz;
    currBlade.v2.xyz = v2_corr.xyz;
    inputBlades[gl_GlobalInvocationID.x] = currBlade;

	// DONE: Cull blades that are too far away or not in the camera frustum
    if (USE_ORIENTATION_CULLING) {
        vec3 camDir = vec3(camera.view[0][2], camera.view[1][2], camera.view[2][2]);
        vec3 bladeDir = vec3(cos(theta), 0.f, sin(theta));
        if (abs(dot(camDir, bladeDir)) > ORIENTATION_CULL_VAL) {
            return;
        }
    }
    
    if (USE_FRUSTUM_CULLING) {
        vec3 m = 0.25 * v0 + 0.5 * v1 + 0.25 * v2;
        vec4 mClip = camera.proj * camera.view * vec4(m, 1.f);
        float limit = mClip.w + FRUSTUM_CULL_VAL;
        if (mClip.x < -limit || mClip.x > limit || mClip.y < -limit || mClip.y > limit || mClip.z < -limit || mClip.z > limit) {
            return;
        }
    }

    if (USE_DISTANCE_CULLING) {
        vec3 camPos = camera.pos.xyz;
        float dproj = length(v0 - camPos - up * dot(v0 - camPos, up));
        if (gl_GlobalInvocationID.x % NUM_BUCKETS > floor(NUM_BUCKETS * (1 - dproj / MAX_DISTANCE))) {
            return;
        }
    }

    //DONE: write blades to the culled blades buffer
	// Note: to do this, you will need to use an atomic operation to read and update numBlades.vertexCount
	// You want to write the visible blades to the buffer without write conflicts between threads
    const uint culledIdx = atomicAdd(numBlades.vertexCount, 1);
    outputBlades[culledIdx] = inputBlades[gl_GlobalInvocationID.x];
}
